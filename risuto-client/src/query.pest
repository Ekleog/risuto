WHITESPACE   =  _{ " " | "\t" | NEWLINE }
  
everything      =  _{ SOI ~ search ~ EOI }
  search        =   { prefix* ~ primary ~ (infix ~ prefix* ~ primary )* }
    infix       =  _{ or | and }
      and       =   { ^"AND"? }
      or        =   { ^"OR" }
    prefix      =  _{ not }
      not       =   { "-" }
    primary     =  _{ archived | done | tag | untagged | today | scheduled | blocked | "(" ~ search ~ ")" | phrase | word }
      archived  =  ${ "archived:" ~ bool }
      done      =  ${ "done:" ~ bool }
      tag       =  ${ "tag:" ~ tagname }
      untagged  =  ${ "untagged:" ~ bool }
      today     =  ${ "today:" ~ bool }
      scheduled =  ${ "scheduled" ~ timecmp ~ timequery }
      blocked   =  ${ "blocked" ~ timecmp ~ timequery }
      phrase    =  ${ "\"" ~ (!"\"" ~ !"\\" ~ ANY | "\\" ~ ANY)* ~ "\"" }
      word      =  ${ (!WHITESPACE ~ !"(" ~ !")" ~ ANY)+ }

bool         =  _{ true | false }
  true       =   { ^"true" }
  false      =   { ^"false" }
int          =  ${ ASCII_DIGIT+ }
date         =  ${ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }
tagname      =  ${ (ASCII_ALPHANUMERIC | ":")+ }
timecmp      =   { ":" | ">=" | "<=" | ">" | "<" }
timequery    =  _{ abstimeq | reltimeq }
  abstimeq   =   { date }
  reltimeq   =   { "today" ~ (reltimeqop ~ int)? }
    reltimeqop =   { "+" | "-" }
