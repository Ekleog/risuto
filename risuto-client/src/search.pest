WHITESPACE   =  _{ " " | "\t" | NEWLINE }
  
everything     =  _{ SOI ~ search ~ EOI }
  search       =   { prefix* ~ primary ~ (infix ~ prefix* ~ primary )* }
    infix      =  _{ or | and }
      and      =   { ^"AND"? }
      or       =   { ^"OR" }
    prefix     =  _{ not }
      not      =   { "-" }
    primary    =  _{ archived | done | tag | "(" ~ search ~ ")" | phrase | word }
      archived =  ${ "archived:" ~ bool }
      done     =  ${ "done:" ~ bool }
      tag      =  ${ "tag:" ~ tagname }
      untagged =  ${ "untagged:" ~ bool }
      phrase   =  ${ "\"" ~ (!"\"" ~ !"\\" ~ ANY | "\\" ~ ANY)* ~ "\"" }
      word     =  ${ (!WHITESPACE ~ !"(" ~ !")" ~ ANY)+ }

bool    = _{ true | false }
  true  = { ^"true" }
  false = { ^"false" }
tagname = { (ASCII_ALPHANUMERIC | ":")+ }
